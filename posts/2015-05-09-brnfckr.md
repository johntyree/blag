---
title: Brnfckr - An optimizing brainfuck toolchain
tags: Hack Haskell Compilers Brainfuck
---

# Brnfckr - An optimizing brainfuck toolchain

I realized this year that I had never written a compiler of any kind
whatsoever. I give myself a slight pass, since I never actually had a course on
compilers, or even studied computer science at all while at school, but it's
about time.

## In the grand tradition of noobs trying to figure out how this all works, I've decided to write a [brainfuck compiler][brnfckr].

If you're not familiar with brainfuck, it looks like this,

```scheme
++++++++++[>+++++++>++++++++++>+++>+<<<<-]>++.>+.+++++++..+++.>++.<<+++++++++++
++++.>.+++.------.--------.>+.
```

Isn't it beautiful?

All kidding aside, it's a great choice for baby's-first-compiler because it's a
tiny language (that's all of it right there), and it's very easy to understand.
It works like this, imagine an array of bytes that goes out to infinity. This
is your memory. Now you can move left or right along that array, you can
increment and decrement values in the array, and you can copy a byte from your
current cell to STDOUT or copy a byte from STDIN to the current cell. That's
it.

```scheme
+  Increment value
-  Decrement value
[  Start of loop, jump past end if current cell is zero
]  End of loop, jump back to start if current cell is nonzero
>  Move to next byte
<  Move to previous byte
.  Write current byte to stdout
,  Read from stdin and write to current byte
```

So in the simplest case you can simply read along the stream of bytes that
defines the program and evaluate it as you go. It's so simple that some folks
don't even try to compile it themselves, they simply allocate a giant block of
bytes and then translate each command directly to C code. That method is both
boring and not particularly educational.

I'm taking a much more traditional approach in my venture. First we parse the
program into an AST representing the operations we want, then we make
one or more passes over the AST to try to speed it up, then we will
either interpret the AST directly or emit code for our target platform.

## Parsing

The first step is to translate our brainfuck code into something with some
structure to it. It's not a particularly interesting AST, because brainfuck
doesn't have any way to fork your execution path. The best you can do is loop,
but there's no way to break out from the middle. Really the AST is more of an
ASList. But I digress, let's start parsing.

I start by defining the type of `Term`, which represents a brainfuck operation.

```haskell
data Term = ValIncr Word8
          | ValDecr Word8
          | ValSet  Word8
          | PtrIncr Int
          | PtrDecr Int
          | ScanIncr
          | ScanDecr
          | Loop [Term]
          | ValInput
          | ValOutput
  deriving (Eq, Show)
```
and a parser from brainfuck characters to `Term`s.

```haskell
parseTerm :: Parser Term
parseTerm = Parser char'
  where char' [] = Left $ ParseFailure "Unexpected EOF"
        char' (x:xs) = case x of
          '+' -> Right (xs, ValIncr 1)
          '-' -> Right (xs, ValDecr 1)
          '<' -> Right (xs, PtrDecr 1)
          '>' -> Right (xs, PtrIncr 1)
          ',' -> Right (xs, ValInput)
          '.' -> Right (xs, ValOutput)
          '[' ->
            case runParser (parseBracket '[' ']') (x:xs) of
              Right (after, "") -> Right (after, Loop [])
              Right (after, inside) -> case runParser (some parseTerm) inside of
                Right (_, terms) -> Right (after, Loop terms)
                Left e -> Left e
              Left e -> Left e
          ']' -> Left $ UnbalancedBracket (x:xs)
          _ -> Left $ ParseFailure [x]
```
I'm not going to go in depth about Applicative/Monadic parsers here because
that topic has been beat to death. I will mention, though, that I reimplemented
the parser from scratch myself rather than use Parsec or Attoparsec. The
entire purpose of this is to get some practice in, right? Here's a sneak peak,
the `Monad` instance of my `Parser` type,

```haskell
data Parser a = Parser { runParser :: String -> Either BrainFuckError (String, a) }

instance Monad Parser where
  return = pure
  fail   s = Parser (const $ Left (ParseFailure s))
  Parser m >>= k = Parser $ \xl ->
    case m xl of
      Left e -> Left e
      Right (xl', a) ->
        let Parser n = k a
        in n xl'
```

So between these, you can see that we're going to build up a value of type
`[Term]`, representing our brainfuck operations.

In the next post, we'll write an interpreter for `[Term]` to make sure things
behave correctly before we start going for optimization.

[brnfckr]: https://github.com/johntyre/brnfckr
